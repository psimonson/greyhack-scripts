//command: decrypt
import_code("/home/5n4k3/src/helper.src")
crypto = get_library("crypto.so", true)
if not crypto then exit

GetPassword = function(userPass)
	if userPass.len != 2 then exit(get_name() + ": wrong syntax")
	password = crypto.decipher(userPass[1])
	return password
end function

if params.len != 1 or params[0] == "-h" or params[0] == "--help" then exit("<b>Usage: " + get_name() + " [hash|file]</b>")

origFile = params[0]
file = get_shell.host_computer.File(origFile)
lines = []

if file then
	if file.is_binary then exit(get_name() + ": can't read " + origFile + ". Binary file")
	if not file.has_permission("r") then exit(get_name() + ": can't read file. Permission denied")
	if file.get_content.len == 0 then exit(get_name() + ": no users found")
	lines = file.get_content.split("\n")
else if origFile.len > 33 then
	lines = [origFile]
else
	exit(get_name() + ": can't find " + origFile)
end if

password = null
if lines.len == 1 then
	print("Single user found.")
	userPass = lines[0].split(":")
	password = GetPassword(userPass)
	if not password then exit("Can't find password :(")
	print("Username: " + userPass[0] + "\nPassword: " + password + "\n")
else
	print("Multiple users found.")
	for line in lines
		userPass = line.split(":")
		password = GetPassword(userPass)
		if not password then
			print("<color=#BB0000>Can't find password :(</color>\n")
		else
			print("<color=#AA8855><b>Password</b> found</color> => " + userPass[0] + " : " + password + "\n")
		end if
	end for
end if

