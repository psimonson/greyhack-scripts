// routerpass.src - Hack router remotely to retrieve password info.
import_code("/home/5n4k3/src/helper.src")

// Function to get etc directory file
get_passwd = function(result)
	if not result then return null
	
	if typeof(result) == "computer" then
		return result.File("/etc/passwd")
	else if typeof(result) == "file" then
		while result.path != "/"
			result = result.parent
		end while
		
		for folder in result.get_folders
			if folder.name == "etc" then
				result = folder
				break
			end if
		end for
		
		for file in result.get_files
			if file.name == "passwd" then
				return file
			end if
		end for
	end if
	return null
end function

if params.len != 2 or params[0] == "-h" or params[0] == "--help" then exit("<b>Usage: " + get_name() + " [public ip] [local ip]</b>")
if not is_valid_ip(params[0]) or not is_valid_ip(params[1]) then exit(get_name() + ": One or more IP address(es) is/are invalid.")

mx = get_library("metaxploit.so", true)
if not mx then exit
lib = use_exploit(mx, params[0])
if not lib then exit

exploits = get_exploits(lib, true, params[1])
if not exploits then exit(get_name() + ": Can't find vulnerabilities.")

// Will show a log after this runs
for exploit in exploits
	address = exploit["address"]
	print("<color=#00CC00>Address >=====[</color><b>" + address + "</b><color=#00CC00>]=====<</color>")
	for value in exploit["values"]
		if value["result"] == null then continue
		
		print("<color=#CC0000>Value >=====[</color><b>" + value["value"] + ":" + value["access"] + "@" + value["type"] + "</b><color=#CC0000>]=====<</color>")
		
		if value["access"] != "guest" and (value["type"] == "computer" or value["type"] == "file") then
			file = get_passwd(value["result"])
			if not file then exit(get_name() + ": Can't find passwd file.")
			if not file.has_permission("r") then exit(get_name() + ": Permission denied.")
			if file.is_binary then exit(get_name() + ": File was a binary.")
			print(file.path + ":\n" + file.get_content)
			exit("Program ended")
		end if
	end for
end for

